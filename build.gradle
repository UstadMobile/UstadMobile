// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonBuilder
import org.json.simple.JSONObject

buildscript {
    ext.version_kotlin = '1.3.41'
    ext.version_shadow = "5.1.0"
    repositories {
        jcenter()
        google()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'com.google.gms:google-services:4.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$version_kotlin"
        classpath "com.github.jengelman.gradle.plugins:shadow:$version_shadow"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()

        maven {
            url "https://devserver2.ustadmobile.com/repo-mvn"
        }
        maven {
            url "https://kotlin.bintray.com/kotlinx"
        }
        maven {
            url "https://dl.bintray.com/soywiz/soywiz"
        }

        maven { url "https://jitpack.io" }
    }
}

subprojects {
    if(Arrays.asList("lib-database-runtime",
            "lib-database-runtime-jdbc", "lib-database-jdbc", "lib-database-retrofitrepository",
            "lib-core-fs", "app-rest-server",
            "core-tests", "lib-content-scrapers").contains(name)) {
        apply plugin: "kotlin"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//Major.minor.database version
version = "0.2.12"
group = "com.ustadmobile.app"

ext {
    ustadVersion = project.version
    ustadVersionCode = 165

    version_gradle_buildconfig_plugin = "1.1.7"
    version_jacoco_tool = "0.8.4"
    version_kotlin_jvmTarget = "1.8"
    version_kotlinx_serialization = "0.11.0"
    version_klock = "1.4.0"
    version_kmp = "0.0.2"
    version_ktor = "1.2.2"
    version_coroutines = "1.2.2"
    version_atomicfu = "0.12.8"
    version_krypto = "1.0.1"

    version_junit = "4.12"
    version_nanohttpd = "2.3.1"
    version_zip4j = "1.3.2"
    version_kxml = "2.3.0"
    version_json = "20160212"
    version_qrcodelibfork = "1.0.3"
    version_okhttp = "3.9.0"
    version_commons_io = "2.5"
    version_gson = "2.8.2"
    version_apache_commons_cli = "1.4"
    version_apache_commons_lang = "3.8.1"
    version_apache_commons_text = "1.6"
    version_apache_commons_dbcp2 = "2.5.0"
    version_mpandroidchart = "v3.0.3"

    version_mockwebserver = "3.10.0"
    version_javapoet = "1.11.1"
    version_reflections = "0.9.11"
    version_simple_jndi = "0.17.2"
    version_sqlite_jdbc = "3.23.1"
    version_jsqlparser = "0.9"
    version_core_mockito = "2.13.0"
    version_kotlin_mockito = "2.1.0"
    version_compressor = "2.1.0"
    version_apache_commons_pool2 = "2.6.0"
    version_jsoup = "1.11.3"
    version_selenium = "3.141.59"
    version_nv_lang = "1.23"
    version_web_driver = "3.6.2"
    version_jersey = "2.27"
    version_retrofit = "2.4.0"
    version_postgres_jdbc = "42.2.5"
    version_log4j = "2.8.2"
    version_exo_player = "2.8.4"
    version_napier = "0.0.6"
    version_tellh_recyclertreeview = "1.2.0"

    version_android_buildtools = "28.0.3"
    version_android_compile_sdk = 28
    version_android_target_sdk = 28
    version_android_min_sdk = 18
    version_android_picasso = 2.71828

    version_android_support_lib = "28.0.0"
    version_androidx_support_lib = "1.0.0"
    version_androidx_core = '1.0.2'
    version_androidx_lifecycle = "2.1.0"
    version_android_room = "2.1.0"
    version_android_multidex = "2.0.0"
    version_android_workmanager = "2.2.0"
    version_android_paging = "1.0.1"
    version_androidx_paging = "2.1.0"
    version_android_constraint_layout = "1.1.3"
    version_android_flexbox = "0.3.2"
    version_android_espresso = "3.1.0"
    version_android_acra = "5.3.0-rc02"
    version_android_junit_runner = "1.1.0"
    version_android_mockito = "2.13.0"
    version_android_roboelectric = "4.0-beta-1"
    version_android_uiautomator = "2.2.0"
    version_android_maven_publish = "3.6.2"
    version_android_firebase_auth = "16.0.4"
    version_android_facebook_login = "4.37.0"
    version_android_twitter_login = "3.3.0"
    version_android_seismic = "1.0.2"
    version_android_fab_extended = "0.0.4"
    version_android_rotatelayout = "3.0.0"

    version_js_node = "10.15.1"
    version_js_yarn="1.13.0"
}

/*
 * Look for a command. If a variable in buildconfig specifies the path, and the file exists, then
 * use the variable value. Otherwise search the system path.
 */
ext.findCommand = {String command, String buildConfigVarName ->
    if(ext.buildConfigProperties.containsKey(buildConfigVarName) && rootProject.file(ext.buildConfigProperties.getProperty(buildConfigVarName)).exists()) {
        return ext.buildConfigProperties.getProperty(buildConfigVarName)
    }else {
        def pathDirs = System.getenv("PATH").split(File.pathSeparator)
        for(path in pathDirs) {
            if(file("$path/$command").exists()) {
                return "$path/$command"
            }
        }
    }

    return "PATHNOTFOUND"
}


ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(project.file("buildconfig.default.properties")))
if(project.file("buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(project.file("buildconfig.local.properties")))
}

ext.localProperties = new Properties()
ext.localProperties.load(new FileInputStream(project.file("local.properties")))


//Map dependencies with the actual packages inside build/js/node_modules
def dependencyMap = ["kotlin-stdlib-js":"kotlin","kmpxmlpullparser-js":"kmpxmlpullparser",
                     "klock-js":"klock","":"kotlin-test","kotlinx-coroutines-core-js":"kotlinx-coroutines-core",
                     "kotlinx-serialization-runtime-js":"kotlinx-serialization-runtime-js",
                     "ktor-client-js":"ktor-ktor-client-js","ktor-client-serialization-js":"ktor-ktor-client-serialization",
                     "napier-js":"napier","lib-room-annotations":"UstadMobile-lib-room-annotations"]
def prepareModuleTasks = []
subprojects { project ->
    afterEvaluate{
        if(project.tasks.findByName("jsJar") != null){
            def fixJsonTask = "fixPackageJsonTask"
            def packageName = "${rootProject.name}-${project.name}"
            def modulePath = "${rootProject.file("build/js/node_modules/${packageName}").path}"
            //add dynamic task to each module except core to fix package.json
            project.task(fixJsonTask){
                doLast{
                    if(!project.name.contains("core")){
                        def jsonObj = new JSONObject()
                        jsonObj.put("main","kotlin/${packageName}.js")
                        jsonObj.put("devDependencies",new JSONObject())

                        def dependencies = new JSONObject()
                        project.configurations.each {
                            if (it.name == "jsMainImplementation") {
                                it.allDependencies.each{ dep ->
                                    dependencies.put(dependencyMap[dep.name],dep.version)
                                }
                                jsonObj.put("dependencies",dependencies)
                            }
                        }
                        jsonObj.put("name","${packageName}")
                        jsonObj.put("version","${rootProject.version}")
                        new File("${modulePath}/package.json").write(new JsonBuilder(jsonObj).toPrettyString())
                    }
                }
            }
            //Run fixJsonTask after running jsJar to each modules
            project.tasks.findByName(fixJsonTask).shouldRunAfter(project.tasks.findByName("jsJar"))
            //Add all dynamic tasks to the list
            prepareModuleTasks.push("${project.path}:${fixJsonTask}")
        }
    }
}

tasks.findByPath(":app-angular:packPreparedModules").dependsOn(tasks.findByPath(":core:build"),prepareModuleTasks)
