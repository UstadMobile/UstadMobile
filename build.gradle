// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.gms:google-services:4.0.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven {
            url "http://devserver2.ustadmobile.com/repo-mvn"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//Major.minor.database version
version = "0.2.8.65L1074"
group = "com.ustadmobile.app"

ext {
    ustadVersion = project.version
    ustadVersionCode = 151

    version_junit = "4.12"
    version_nanohttpd = "2.3.1"
    version_zip4j = "1.3.2"
    version_kxml = "2.3.0"
    version_json = "20160212"
    version_qrcodelibfork = "1.0.3"
    version_okhttp = "3.9.0"
    version_commons_io = "2.5"
    version_ascii85 = "1.1"
    version_gson = "2.8.2"
    version_apache_commons_cli = "1.4"
    version_apache_commons_lang = "3.8.1"
    version_apache_commons_text = "1.6"

    version_mockwebserver = "3.10.0"
    version_javapoet = "1.11.1"
    version_reflections = "0.9.11"
    version_simple_jndi = "0.17.2"
    version_sqlite_jdbc = "3.23.1"
    version_jsqlparser = "0.9"
    version_core_mockito = "2.13.0"
    version_compressor = "2.1.0"
    version_apache_commons_pool2 = "2.6.0"
    version_jsoup = "1.11.3"
    version_selenium = "3.14.0"
    version_nv_lang = "1.23"
    version_web_driver = "3.0.0"
    version_jersey = "2.27"
    version_retrofit = "2.4.0"
    version_postgres_jdbc = "42.2.5"
    version_log4j = "2.8.2"

    version_android_buildtools = "28.0.3"
    version_android_compile_sdk = 28
    version_android_target_sdk = 28
    version_android_min_sdk = 16

    version_android_support_lib = "28.0.0"
    version_android_lifecycle = "1.1.1"
    version_android_room = "1.1.1"
    version_android_multidex = "1.0.3"
    version_android_workmanager = "1.0.0-beta02"
    version_android_paging = "1.0.1"
    version_android_constraint_layout = "1.1.3"
    version_android_flexbox = "0.3.2"
    version_android_espresso = "3.0.2"
    version_android_acra = "5.1.1"
    version_android_junit_runner = "1.0.2"
    version_android_mockito = "2.13.0"
    version_android_uiautomator = "2.1.3"
    version_android_maven_publish = "3.6.2"
    version_android_firebase_auth = "16.0.4"
    version_android_facebook_login = "4.37.0"
    version_android_twitter_login = "3.3.0"

}

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(project.file("buildconfig.default.properties")))
if(project.file("buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(project.file("buildconfig.local.properties")))
}

ext.localProperties = new Properties()
ext.localProperties.load(new FileInputStream(project.file("local.properties")))

//Due to a bug, the JDK8 annotation processor provides incorrect parameter names, thus we need
// to use JDK9+ to run the annotation processor https://bugs.openjdk.java.net/browse/JDK-8191074
if(!ext.localProperties.containsKey("javac.9plus")) {
    throw new GradleException("Due to a JDK8 bug ( https://bugs.openjdk.java.net/browse/JDK-8191074 ) " +
            "with providing parameter names to the annotation processor, you must define a path to a " +
            "JDK9+ javac for use with the database annotation processor in local.properties " +
            "e.g. javac.9plus=/path/to/jvm9/bin/javac")
}

/**
 * Local configuration management
 *
 * Run gradle with -PconfigDst=path/to/dir to specify where to copy to. This will default to a directory
 * parallel to the source directory called UstadMobile-config-<localConfigId> where localConfigId
 * is the config id in the buildconfig.local.properties
 */
task copyLocalConfigToRepoDir(type: Copy) {
    from("./") {
        include "buildconfig.local.properties"
        include "app-*/buildconfig.local.properties"

        include "app-android/src/localconfig/**"
        include "app-j2me/res-local/**"
        include "core/src/localconfig/**"
    }

    into project.hasProperty("configDst") ? configDst : "../UstadMobile-config-" + project.ext.buildConfigProperties["localConfigId"]
}

task copyLocalConfigFromRepoDir(type: Copy) {
    from(project.ext.has("localConfigDir") ? localConfigDir :  "../UstadMobile-config-" + project.ext.buildConfigProperties["localConfigId"]) {
        include "buildconfig.local.properties"
        include "app-*/buildconfig.local.properties"

        include "app-android/src/localconfig/**"
        include "app-j2me/res-local/**"
    }

    into "./"
}




