plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', '**/*.kotlin_metadata', 'META-INF/*.kotlin_module', 'licenses/**.TXT']
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.version_android_target_sdk
    }
    namespace 'com.ustadmobile.lib.test'
}

sourceCompatibility = '17'
targetCompatibility = '17'

group rootProject.group
version rootProject.version

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }

        compilations.test.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }
    }
   // jvm("commonJvm")

    android()

    /* JS is disabled until after kotlin 1.4 upgrade
    js {
        nodejs()

        compilations.main.kotlinOptions {
            metaInfo = true
            sourceMap = true
            moduleKind = "commonjs"
        }
    }
    */


    sourceSets {
        commonMain {
            dependencies {
                implementation project(":core")
                implementation libs.kotlin.stdlib.common
                implementation libs.coroutines
                implementation libs.ktor.client.core
                implementation libs.ktor.client.json
                implementation libs.kotlinx.serialization
                implementation libs.napier
                implementation libs.kodein.di
                implementation project(":lib-database")
                compileOnly libs.door.runtime
                compileOnly project(":lib-util")
            }
        }

        commonJvmMain {
            dependencies {
                implementation libs.ktor.client.okhttp

                compileOnly libs.door.runtime
                compileOnly project(":lib-util")
            }
        }

        jvmMain {
            dependsOn commonJvmMain
            dependencies {
                implementation libs.ktor.server.servlet
                implementation libs.ktor.server.netty

                implementation libs.okhttp
                implementation libs.mockwebserver
                implementation libs.sqlite.jdbc
                implementation libs.mockito.kotlin
            }
        }

        androidMain {
            dependsOn commonJvmMain
            dependencies {
                implementation libs.kotlinx.coroutines.android
                implementation libs.androidx.room.runtime
            }
        }


    }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}
