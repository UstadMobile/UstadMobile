plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.multiplatform'
}

android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk
    buildToolsVersion rootProject.ext.version_android_buildtools

    defaultConfig {
        minSdkVersion rootProject.ext.version_android_min_sdk
        targetSdkVersion rootProject.ext.version_android_target_sdk
    }
}

apply plugin: "maven-publish"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group rootProject.group
version rootProject.version

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }

        compilations.test.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }
    }
   // jvm("commonJvm")

    android()

    js {
        nodejs()

        compilations.main.kotlinOptions {
            metaInfo = true
            sourceMap = true
            moduleKind = "commonjs"
        }
    }


    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$version_coroutines"
                implementation "com.soywiz:klock-metadata:$version_klock"
                compileOnly project(":lib-database-entities")
                compileOnly project(":lib-database")
                compileOnly project(":lib-door-runtime")
                compileOnly project(":lib-util")
            }
        }

        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$version_coroutines"
                implementation "com.soywiz:klock-js:$version_klock"
            }
        }
        commonJvmMain {
            dependencies {
                implementation "io.ktor:ktor-server-servlet:$version_ktor"
                implementation "io.ktor:ktor-server-netty:$version_ktor"
                implementation "io.ktor:ktor-gson:$version_ktor"
                compileOnly project(":app-ktor-server")
                compileOnly project(":lib-database-entities")
                compileOnly project(":lib-database")
                compileOnly project(":lib-door-runtime")
                compileOnly project(":lib-util")
            }
        }

        jvmMain {
            dependsOn commonJvmMain
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
                implementation "com.soywiz:klock-jvm:$version_klock"
            }
        }

        androidMain {
            dependsOn commonJvmMain
            kotlin.srcDirs += ["src/commonJvmMain/kotlin"]
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutines"
                implementation "com.soywiz:klock-android:$version_klock"
                implementation project(":lib-database-android")
                implementation "androidx.room:room-runtime:$version_android_room"
            }
        }


    }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}