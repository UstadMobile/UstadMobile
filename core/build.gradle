import java.util.concurrent.atomic.AtomicInteger
import java.nio.file.Files
import java.nio.file.Path

/**
 * Core module build script : see buildconfig.core.default.gradle for configuration
 */

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.7'
    }
}

apply plugin: "java-library"
apply plugin: "jacoco"
apply plugin: "maven-publish"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar {
    //Exclude factory classes that are intended to be replaced by platform specific implementations
    exclude("com/ustadmobile/core/impl/*Factory.class")
    exclude("com/ustadmobile/core/db/impl/*Factory.class")
    exclude("com/ustadmobile/core/db/entities/*.class")
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava

}

repositories {
    mavenCentral()
    maven {
        url "http://devserver2.ustadmobile.com/repo-mvn"
    }
}

dependencies {
    implementation "org.json:json:20160212"
    implementation "net.sf.kxml:kxml2:2.3.0"
    implementation 'com.ustadmobile.qrcodelibomrfork:qrcode-core:1.0-SNAPSHOT'
    compileOnly project(":lib-database")
    implementation project(":lib-database-annotation")
    implementation project(":lib-util")

    project.ext.nanoLrsVersion = rootProject.ext.nanoLrsVersion
    api "com.ustadmobile.nanolrs:nanolrs-core:$nanoLrsVersion"

    testImplementation project(":core-tests")
    testImplementation project(":sharedse")
    testImplementation project(":app-javase")
    testImplementation project(":lib-core-fs-tests")
    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
    testImplementation 'org.nanohttpd:nanohttpd-nanolets:2.3.1'
    testImplementation project(":sharedse-tests")
    testImplementation 'junit:junit:4.12'
}


test {
    testLogging {
        exceptionFormat = 'full'
    }
}

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(rootProject.file("buildconfig.default.properties")))
if(rootProject.file("buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(rootProject.file("buildconfig.local.properties")))
}



/**
 * Note: This is compiled into the main source directory (then gitignore'd) because Android studio
 * will otherwise mark it as an unresolved reference if we use the default generated sources directory.
 */
task generateCoreBuildConfig (type: de.fuerstenau.gradle.buildconfig.GenerateBuildConfigTask) {
    inputs.files(fileTree(dir: rootProject.file(".")).matching { include 'buildconfig.*.properties' })
    outputDir = new File ("${projectDir}/src/main/java")
    packageName = "com.ustadmobile.core.buildconfig"
    appName = project.ext.buildConfigProperties['appName']
    clsName = "CoreBuildConfig"
    version = rootProject.version

    ArrayList<String> baseMenuTypes = new ArrayList<>()
    baseMenuTypes.add("guest")
    baseMenuTypes.add("authenticated")

    long buildTimeMillis = Calendar.getInstance().getTimeInMillis()

    for(int i = 0; i < baseMenuTypes.size(); i++) {
        String menuTypePrefix = "baseMenu." + baseMenuTypes.get(i)
        int menuItemIndex = 0
        String menuItemTitle=null
        ArrayList<String> titleIdsList = new ArrayList<>()
        ArrayList<String> destinationsList = new ArrayList<>()
        ArrayList<String> iconList = new ArrayList<>()

        while((menuItemTitle = project.ext.buildConfigProperties.getProperty(menuTypePrefix + "." + menuItemIndex + ".title")) != null) {
            String itemPrefix = menuTypePrefix + "." + menuItemIndex
            String itemDest = project.ext.buildConfigProperties.getProperty(itemPrefix + ".dest")
            if(itemDest == null || itemDest.equals("null")) {
                break
            }

            titleIdsList.add(menuItemTitle)
            destinationsList.add(itemDest)
            iconList.add(project.ext.buildConfigProperties.getProperty(itemPrefix + ".icon"))
            menuItemIndex++
        }

        String basePointMenuArrayStr = "new com.ustadmobile.core.view.BasePointMenuItem[]{"
        for(int j = 0; j< titleIdsList.size(); j++) {
            String titleIdVal = titleIdsList.get(j)
            if(titleIdsList[i].startsWith("MessageID")) {
                titleIdVal = "com.ustadmobile.core.generated.locale." + titleIdVal
            }
            basePointMenuArrayStr += "new com.ustadmobile.core.view.BasePointMenuItem("+ titleIdVal + ", \"" + destinationsList.get(j) + "\", " + (iconList.get(j) != null ? "\""+iconList.get(j) + "\"": "null") +")"
            if(j < titleIdsList.size()-1){
                basePointMenuArrayStr += ", "
            }
        }
        basePointMenuArrayStr += "}"

        buildConfigField "com.ustadmobile.core.view.BasePointMenuItem[]", "BASEPOINT_MENU_" + baseMenuTypes.get(i).toUpperCase(),basePointMenuArrayStr
    }


    String[] supportedLocales = project.ext.buildConfigProperties.getProperty("appSupportedLocales").split(",")
    String supportedLocalesValue = "new String[]{"
    for(int k = 0; k < supportedLocales.length; k++) {
        supportedLocalesValue += "\"" + supportedLocales[k] + "\""
        if(k != supportedLocales.length-1)
            supportedLocalesValue += ","
    }
    supportedLocalesValue += "}"

    buildConfigField "String[]", "SUPPORTED_LOCALES", supportedLocalesValue

    buildConfigField "String", "BASE_NAME", project.ext.buildConfigProperties["baseName"]
    buildConfigField "long", "BUILD_TIME_MILLIS", String.valueOf(buildTimeMillis) + "L"
    buildConfigField "boolean", "OPDS_ITEM_ENABLE_BACKGROUNDS", project.ext.buildConfigProperties["appOpdsBackgroundsEnable"]
    buildConfigField "boolean", "BASEPOINT_BROWSEBUTTON_ENABLED", project.ext.buildConfigProperties["appBasepointBrowseButtonUrl"].equals("") ? "false" : "true"
    buildConfigField "boolean", "BASEPOINT_FILTER_BY_UI_LANG", project.ext.buildConfigProperties["appBasepointFilterByUiLang"]
    buildConfigField "boolean", "LOGIN_TITLEBAR_VISIBLE", project.ext.buildConfigProperties["appLoginTitleBarVisible"]
    buildConfigField "boolean", "LOGIN_BEFORE_DOWNLOAD", project.ext.buildConfigProperties["appLoginBeforeDownload"]
    buildConfigField "String", "DEFAULT_PREFERRED_ACQUISITION_FORMATS", project.ext.buildConfigProperties["appPreferredAcquisitionFormats"]
    buildConfigField "int", "ACQUISITION_SELECT_WEIGHT_LANGUAGE", project.ext.buildConfigProperties['appAcquisitionLanguagePrefWeight']
    buildConfigField "int", "ACQUISITION_SELECT_WEIGHT_MIMETYPE", project.ext.buildConfigProperties['appAcquisitionMimeTypeWeight']
    buildConfigField "String", "NETWORK_SERVICE_TYPE", project.ext.buildConfigProperties["appNetworkServiceType"]
    buildConfigField "String", "WIFI_P2P_INSTANCE_NAME", project.ext.buildConfigProperties["appP2pServiceInstanceName"]
    buildConfigField "String", "DEFAULT_XAPI_SERVER", project.ext.buildConfigProperties["appDefaultXapiServer"]
    buildConfigField "String", "DEFAULT_XAPI_SERVER_NOSSL", project.ext.buildConfigProperties["appDefaultXapiServerNoSsl"]
    buildConfigField "boolean", "WELCOME_DIALOG_ENABLED", project.ext.buildConfigProperties["appWelcomeDialgEnabled"]
    buildConfigField "boolean", "EPUB_TOC_ENABLED", project.ext.buildConfigProperties["appEpubTocEnabled"]

}
compileJava.dependsOn(generateCoreBuildConfig)

task prepareLocale (type: de.fuerstenau.gradle.buildconfig.GenerateBuildConfigTask) {
    inputs.files(fileTree(dir: rootProject.file("locale")).matching { include '*.xml' })
    outputDir = new File("${projectDir}/src/main/java")
    packageName = "com.ustadmobile.core.generated.locale"

    appName = project.ext.buildConfigProperties['appName']
    clsName = "MessageID"

    Map<String, Integer> messageIds = new HashMap<>();
    AtomicInteger messageIdGenerator = new AtomicInteger(2000)

    rootProject.fileTree(dir : "core/locale/main/values", include: "*.xml").each() { File file ->
        def xmlDoc = new XmlSlurper().parseText(file.getText("UTF-8"))
        xmlDoc.string.each() { str ->
            String msgName = str.@name

            if(!messageIds.containsKey(msgName)) {
                Integer value;
                if(str.attributes().get("value") != null) {
                    value = new Integer(str.attributes().get("value"))
                }else {
                    value = new Integer(messageIdGenerator.getAndIncrement())
                }

                messageIds.put(msgName, value)
            }
        }
    }

    Iterator<String> nameIterator = messageIds.keySet().iterator();
    while(nameIterator.hasNext()) {
        String strName = nameIterator.next()
        Integer value =messageIds.get(strName)
        System.out.println("buildconfigfield : '" + strName + "' = "+ String.valueOf(value))

        buildConfigField "int", strName, String.valueOf(value)
    }
}
compileJava.dependsOn(prepareLocale)

/*
 * Generate .properties version of the core locale files, a class that extends GWT Messages, and
 * a helper with a switch statement that will retrieve a String value given the numerical message id.
 */
task generateLocaleAsPropertiesFiles {
    inputs.files("core/locale/*/*.xml")
    doLast {
        //map in the form of filename-localename
        HashMap<String, Properties> mainPropsMap = new HashMap()
        File outDir = new File("${projectDir}/build/generated/locale-properties")

        if(!outDir.exists()) {
            outDir.mkdirs()
        }

        File mainOutDir = new File(outDir, "main")
        if(!mainOutDir.exists()) {
            mainOutDir.mkdir()
        }

        File mainPropsOutDir = new File(mainOutDir, "props")
        if(!mainPropsOutDir.exists()) {
            mainPropsOutDir.mkdir()
        }

        rootProject.fileTree(dir : "core/locale/main", include: "**/*.xml").each() { File file ->
            def xmlDoc = new XmlSlurper().parseText(file.getText("UTF-8"))

            String parentName = file.getParentFile().getName()
            System.out.println("process:" + file + " from " + parentName)

            String localeSuffix = ""
            if(parentName.contains("-")) {
                localeSuffix = "_" + parentName.substring(parentName.indexOf("-")+1)
            }

            Properties props = new Properties()
            String baseName = file.getName().substring(0, file.getName().length() - 4)//chop off ".xml"
            baseName += localeSuffix

            System.out.println("Generating: " + baseName + ".properties")
            mainPropsMap.put(baseName, props)

            xmlDoc.string.each() { str ->
                String msgName = str.@name
                String msgValue = str.text()
                props.setProperty(msgName, msgValue)
            }

            File outFile = new File(mainPropsOutDir, baseName + ".properties")
            props.store(new FileWriter(outFile), "Generated: DO NOT MODIFY : see core/locale")
        }

        File localconfigOutDir = new File(outDir, "localconfig")
        if(!localconfigOutDir.exists()) {
            localconfigOutDir.mkdir()
        }

        File localconfigPropsOutDir = new File(localconfigOutDir, "props")
        if(!localconfigPropsOutDir.exists()) {
            localconfigPropsOutDir.mkdir()
        }

        rootProject.fileTree(dir : "core/locale/localconfig", include: "**/*.xml").each() { File file ->
            def xmlDoc = new XmlSlurper().parseText(file.getText("UTF-8"))

            String parentName = file.getParentFile().getName()
            System.out.println("process:" + file + " from " + parentName)

            String localeSuffix = ""
            if(parentName.contains("-")) {
                localeSuffix = "_" + parentName.substring(parentName.indexOf("-")+1)
            }

            String baseName = file.getName().substring(0, file.getName().length() - 4)//chop off ".xml"
            baseName += localeSuffix

            Properties props = new Properties()
            Properties mainprops = mainPropsMap.get(baseName)
            Enumeration<String> mainKeys = mainprops.keys()
            while(mainKeys.hasMoreElements()) {
                String propName = mainKeys.nextElement()
                String propValue = mainprops.get(propName)
                props.setProperty(propName, propValue)
            }

            xmlDoc.string.each() { str ->
                String msgName = str.@name
                String msgValue = str.text()
                props.setProperty(msgName, msgValue)
            }

            File outFile = new File(localconfigPropsOutDir, baseName + ".properties")
            props.store(new FileWriter(outFile), "Generated: DO NOT MODIFY : see core/locale")
        }




        Set<String> mainMessageIds = new HashSet<>()
        rootProject.fileTree(dir : "core/locale/main/values", include: "*.xml").each() { File file ->
            def xmlDoc = new XmlSlurper().parseText(file.getText("UTF-8"))
            xmlDoc.string.each() { str ->
                String messageName = str.@name
                mainMessageIds.add(messageName)
            }
        }

        Set<String> localMessageIds = new HashSet<>()
        localMessageIds.addAll(mainMessageIds)
        rootProject.fileTree(dir : "core/locale/localconfig/values", include: "*.xml").each() { File file ->
            def xmlDoc = new XmlSlurper().parseText(file.getText("UTF-8"))
            xmlDoc.string.each() { str ->
                String messageName = str.@name
                localMessageIds.add(messageName)
            }
        }



        //now generate the required Java sources for GWT
        File[] outDirs = new File[2]
        outDirs[0] = new File(mainOutDir, "java")
        outDirs[1] = new File(localconfigOutDir, "java")

        Set<String>[] messageIdSets = new Set<>[2]
        messageIdSets[0] = mainMessageIds
        messageIdSets[1] = localMessageIds

        for(int i = 0; i < outDirs.length; i++) {
            if(!outDirs[i].exists()) {
                outDirs[i].mkdirs()
            }

            StringBuilder msgInterface = new StringBuilder()
            msgInterface.append("package com.ustadmobile.core.generated.locale;\n")
            msgInterface.append("public interface UmMessages extends com.google.gwt.i18n.client.Messages{\n")


            StringBuilder msgHelper = new StringBuilder()
            msgHelper.append("package com.ustadmobile.core.generated.locale;\n")
                    .append("public class UmMessagesHelper {\n")
                    .append("\tpublic static String getMessage(int messageCode, UmMessages messages) {\n")
                    .append("\t\tswitch(messageCode){\n")

            Iterator<String> messageIdIterator = messageIdSets[i].iterator()
            while(messageIdIterator.hasNext()) {
                String messageId = messageIdIterator.next()

                msgInterface.append("\tString " + messageId +"();\n\n")

                msgHelper.append("\t\t\tcase MessageID." + messageId + ":\n")
                        .append("\t\t\t\treturn messages." + messageId +"();\n")
            }

            msgInterface.append("\n}\n")
            msgHelper.append("\t\t}\n\t}\n}")

            File pkgDir = new File(outDirs[i], "java/com/ustadmobile/core/generated/locale")
            if(!pkgDir.exists()) {
                pkgDir.mkdirs()
            }

            Path msgInterfacePath = new File(pkgDir, "UmMessages.java").toPath()
            Files.write(msgInterfacePath, msgInterface.toString().getBytes("UTF-8"))

            Path msgHelperPath = new File(pkgDir, "UmMessagesHelper.java").toPath()
            Files.write(msgHelperPath, msgHelper.toString().getBytes("UTF-8"))
        }





    }


}

/*
 * Added to overcome limitation on Android studio as per:
 *  http://tools.android.com/knownissues#TOC-JUnit-tests-missing-resources-in-classpath-when-run-from-Studio
 */
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    from "${projectDir}/../core-tests/src/main/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources


publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.app'
            artifactId 'core'
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}
