import java.util.concurrent.atomic.AtomicInteger

/**
 * Core module build script : see buildconfig.core.default.gradle for configuration
 */


group rootProject.group
version rootProject.version

apply plugin: "kotlin-multiplatform"
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.android.library'
apply plugin: "maven-publish"
apply plugin: "jacoco"
apply plugin: 'com.github.johnrengelman.shadow'


android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk
    buildToolsVersion rootProject.ext.version_android_buildtools

    defaultConfig {
        minSdkVersion rootProject.ext.version_android_min_sdk
        targetSdkVersion rootProject.ext.version_android_target_sdk
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

/*
 * This might be required to be able to run unit tests via the IDE.
 */
task copyTestResources(type: Copy) {
    outputs.upToDateWhen {
        project.file("build/local.env.properties").exists()
    }

    from project.file("src/commonTest/resources")
    into rootProject.file("build/classes/test/core_jvmTest")

    doFirst {
        if(!project.file("build").exists())
            project.file("build").mkdir()

        def outWriter = new FileWriter(project.file("build/local.env.properties"))
        System.getProperties().store(outWriter, "System properties for use with running unit tests in IDE")
        outWriter.flush()
        outWriter.close()
    }
}
/*
 * Generate a class with constant integer flags for each string message ID. See localization docs
 * for further information.
 */
task prepareLocale {
    inputs.files(fileTree(dir: project.file("locale")).matching { include '*.xml' })


    doFirst {
        File outFile = project.file("build/generated/source/umlocale/com/ustadmobile/core/generated/locale/MessageID.kt")
        if(!outFile.parentFile.exists()) {
            outFile.parentFile.mkdirs()
        }
        outFile.text = ''
        outFile << 'package com.ustadmobile.core.generated.locale\n\n'
        outFile << '/** DO NOT EDIT. GENERATED CODE */\n'
        outFile << 'object MessageID{\n\n'

        Map<String, Integer> messageIds = new HashMap<>()
        AtomicInteger messageIdGenerator = new AtomicInteger(2000)

        rootProject.fileTree(dir: "core/locale/main/values", include: "*.xml").each() { File file ->
            def xmlDoc = new XmlSlurper().parseText(file.getText("UTF-8"))
            xmlDoc.string.each() { str ->
                String msgName = str.@name

                if (!messageIds.containsKey(msgName)) {
                    Integer value;
                    if (str.attributes().get("value") != null) {
                        value = new Integer(str.attributes().get("value"))
                    } else {
                        value = new Integer(messageIdGenerator.getAndIncrement())
                    }

                    messageIds.put(msgName, value)
                }
            }
        }

        Iterator<String> nameIterator = messageIds.keySet().iterator()

        while (nameIterator.hasNext()) {
            String strName = nameIterator.next()
            Integer value = messageIds.get(strName)
            outFile << '    const val ' + strName + ' = ' + String.valueOf(value) + '\n\n'
        }
        outFile << '}'
    }
}


tasks.whenTaskAdded { task ->
    if (task.name == "compileTestKotlinJvm") {
        task.dependsOn(copyTestResources)
    }

    if(task.name.startsWith("compile") || task.name.startsWith("assemble") || task.name.endsWith("Jar") || task.name.startsWith("build")) {
        task.dependsOn(prepareLocale)
    }
}

gradle.projectsEvaluated {
    preBuild.dependsOn(prepareLocale)
}



kotlin {

    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }

        compilations.test.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }
    }

    android()

    js("js") {
        nodejs()


        compilations.main.kotlinOptions {
            metaInfo = true
            sourceMap = true
            moduleKind = "commonjs"
        }
    }



    sourceSets {
        commonMain {
            kotlin.srcDirs += "build/generated/source/umlocale/"
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$version_kotlinx_serialization"
                implementation "com.soywiz:klock-metadata:$version_klock"
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-metadata:$version_kmp"
                implementation "io.ktor:ktor-client-core:$version_ktor"
                implementation "io.ktor:ktor-client-json:$version_ktor"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$version_coroutines"
                implementation "com.github.aakira:napier:$version_napier"
                implementation "org.jetbrains.kotlinx:atomicfu-common:$version_atomicfu"
                implementation project(':lib-door-runtime')
                compileOnly project(':lib-database')
                compileOnly project(':lib-database-entities')
                compileOnly project(":lib-room-annotations")
                implementation project(":lib-util")
            }
        }

        commonJvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
                implementation "io.ktor:ktor-client-okhttp:$version_ktor"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-jvm:$version_kmp"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$version_kotlinx_serialization"
                implementation "org.jetbrains.kotlinx:atomicfu:$version_atomicfu"
                implementation "io.ktor:ktor-client-gson:$version_ktor"

                compileOnly project(':lib-database')
                compileOnly project(':lib-database-entities')

            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
                implementation "com.github.aakira:napier-jvm:$version_napier"
                implementation "com.soywiz:klock-jvm:$version_klock"
            }
            dependsOn commonJvmMain
        }

        commonTest {
            dependencies {
                implementation project(":lib-database-mpp")
                implementation project(":lib-database-entities")
                implementation project(":lib-database")
                implementation project(":lib-test-common")
            }
        }

        jvmTest {
            dependencies {
                implementation "junit:junit:$version_junit"
                implementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$version_kotlin_mockito"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"

                implementation "com.github.h-thurow:simple-jndi:$version_simple_jndi"
                implementation "org.apache.commons:commons-pool2:$version_apache_commons_pool2"
                implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
                implementation "org.apache.commons:commons-dbcp2:$version_apache_commons_dbcp2"

                implementation "org.nanohttpd:nanohttpd:$version_nanohttpd"
                implementation "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"

                implementation "com.squareup.okhttp3:mockwebserver:$version_mockwebserver"
                implementation "io.ktor:ktor-server-netty:$version_ktor"

                /*
                  For this to work in the IDE, these need to be API instead of implementation
                 */
                api project(":sharedse")
                api project(":lib-room-annotations")
                api project(":lib-database-annotations")
                api project(":lib-database-mpp")
                api project(":lib-test-common")
                api project(":app-ktor-server")

                // 5/June/2019 - Unfortunately, Android Studio won't load these depnendencies
                // of sharedse when running tests in the IDE, which is why we have them here too.

                //Begin SharedSE Android and JVM dependencies
                implementation "org.nanohttpd:nanohttpd:$version_nanohttpd"
                implementation "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"
                implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
                implementation "net.sf.kxml:kxml2:$version_kxml"
                implementation "com.google.code.gson:gson:$version_gson"
                implementation "net.lingala.zip4j:zip4j:$version_zip4j"
                //end SharedSE Android and JVM dependencies
            }
        }
        androidMain {
            dependencies {
                implementation "com.github.aakira:napier-android:$version_napier"
                implementation "androidx.appcompat:appcompat:$version_androidx_support_lib"
                implementation "androidx.legacy:legacy-support-v4:$version_androidx_support_lib"
                implementation "com.soywiz:klock-android:$version_klock"
                implementation "androidx.room:room-runtime:$version_android_room"
                implementation "androidx.paging:paging-runtime:$version_androidx_paging"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutines"
                implementation project(":lib-database-android")
                implementation "org.jsoup:jsoup:$version_jsoup"
                //Image loading library
                implementation "com.squareup.picasso:picasso:$version_android_picasso"

            }
            dependsOn commonJvmMain
        }

        androidTest{
            dependencies{
                implementation "junit:junit:$version_junit"
                implementation "io.ktor:ktor-server-netty:$version_ktor"
                implementation "com.squareup.okhttp3:mockwebserver:$version_mockwebserver"
                implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
                implementation "com.github.aakira:napier-android:$version_napier"
                implementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$version_kotlin_mockito"

                implementation "com.github.h-thurow:simple-jndi:$version_simple_jndi"
                implementation "org.apache.commons:commons-pool2:$version_apache_commons_pool2"
                implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
                implementation "org.apache.commons:commons-dbcp2:$version_apache_commons_dbcp2"

                implementation "androidx.room:room-runtime:$version_android_room"
                implementation "androidx.test:runner:$version_android_junit_runner"
                implementation "androidx.test:rules:$version_android_junit_runner"

                implementation "org.nanohttpd:nanohttpd:$version_nanohttpd"
                implementation "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"

                api project(":sharedse")
                api project(":lib-room-annotations")
                api project(":core")
                api project(":lib-database-annotations")
                api project(":lib-database-android")
                api project(":lib-door-runtime")
                api project(":lib-database-mpp")

                implementation "org.robolectric:robolectric:$version_android_roboelectric"
            }
        }

        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$version_kotlin"
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-js:$version_kmp"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$version_kotlinx_serialization"
                implementation "com.soywiz:klock-js:$version_klock"
                implementation "com.github.aakira:napier-js:$version_napier"
                implementation "io.ktor:ktor-client-js:$version_ktor"
                implementation "io.ktor:ktor-client-serialization-js:$version_ktor"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$version_coroutines"
            }
        }

    }
}

jacoco {
    toolVersion = "$version_jacoco_tool"
}

/**
 * Create a Jacocco task for reporting test coverage - as per:
 * https://medium.com/@aldychris/kotlin-multiplatform-for-ios-and-android-mobile-application-96a753e175f7
 */
project.afterEvaluate {
    def testTaskName = "jvmTest"

    // Create gradle task
    task "jvmJacocoTestReport" (type:JacocoReport, dependsOn: "$testTaskName") {
        group = "Reporting"
        description = "Generate Jacoco coverage reports on the common module build."

        def excludes = [
                '**/*Test*.*'
        ]

        classDirectories = fileTree(
                dir: "${project.buildDir}/classes/kotlin/jvm/",
                excludes: excludes
        )

        def coverageSourceDirs = [
                "src/commonMain/kotlin"
        ]

        additionalSourceDirs = files(coverageSourceDirs)
        sourceDirectories = files(coverageSourceDirs)
        executionData = files("${project.buildDir}/jacoco/jvmTest.exec")

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(rootProject.file("buildconfig.default.properties")))
if (rootProject.file("buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(rootProject.file("buildconfig.local.properties")))
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}

