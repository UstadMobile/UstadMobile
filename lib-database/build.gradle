/*
 * Note: If the Android plugin is enabled, since Gradle 5.4, the annotation processor will not run
 * with kapt (using project or files to locate the annotation processor).
 */


group rootProject.group
version rootProject.version


apply plugin: "kotlin-multiplatform"
//apply plugin: 'kotlinx-serialization'
apply plugin: "maven-publish"
apply plugin: "jacoco"
apply plugin: "kotlin-kapt"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group rootProject.group
version rootProject.version

kotlin {

    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }

        compilations.test.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "$version_kotlin_jvmTarget"
        }
    }

    js {
        nodejs()

        compilations.main.kotlinOptions {
            metaInfo = true
            sourceMap = true
            moduleKind = "commonjs"
        }
    }


    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$version_kotlin"
                //implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$version_kotlinx_serialization"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$version_coroutines"
                implementation project(":lib-util")
                implementation project(":lib-database-entities")
                implementation project(':lib-door-runtime')
                implementation project(":lib-database-annotations")
                compileOnly project(":lib-room-annotations")

            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
                //implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$version_kotlinx_serialization"
            }
        }
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$version_kotlin"
                //implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$version_kotlinx_serialization"
                implementation "com.soywiz:klock-js:$version_klock"
            }
        }

    }

}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}


kapt {
    arguments {
        arg("doordb_android_out", rootProject.file("lib-database-android/build/generated/source/door").absolutePath)
        arg("doordb_jvm_out", rootProject.file("lib-database-mpp/build/generated/source/door").absolutePath)
        arg("doordb_ktor_out", rootProject.file("app-ktor-server/build/generated/source/door").absolutePath)
        arg("doordb_source_path", project.file("src/commonMain/kotlin"))
    }
}


dependencies {
    /**
     * Since Gradle 5.4 using kapt project(":...") does not work.
     */
    kapt files("../lib-database-annotation-processor/build/libs/lib-database-annotation-processor.jar",
            "../lib-door-runtime/build/libs/lib-door-runtime-jvm.jar")
    kapt "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
    kapt "com.squareup:kotlinpoet:1.2.0"
    kapt "com.github.jsqlparser:jsqlparser:$version_jsqlparser"
    kapt "io.ktor:ktor-server-netty:$version_ktor"
    kapt "org.apache.commons:commons-text:$version_apache_commons_text"
    kapt "com.google.code.gson:gson:$version_gson"
    kapt "io.ktor:ktor-client-core:$version_ktor"
    kapt "io.ktor:ktor-client-json:$version_ktor"
    kapt "io.ktor:ktor-client-okhttp:$version_ktor"
}

compileKotlinJvm.dependsOn(":lib-database-annotation-processor:jar")

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}