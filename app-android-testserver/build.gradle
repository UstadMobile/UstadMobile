apply plugin: 'com.android.application'

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(project.file("../app-android/buildconfig.default.properties")))
if (project.file("../app-android/buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(project.file("../app-android/buildconfig.local.properties")))
}



android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk
    buildToolsVersion rootProject.ext.version_android_buildtools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    defaultConfig {
        applicationId "com.toughra.ustadmobile.testserver"
//        multiDexEnabled true
        minSdkVersion 16
        targetSdkVersion rootProject.ext.version_android_target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "config"

    productFlavors {
        /*
         * The localconfig flavor is created based on the buildconfig.local.properties settings
         */
        localconfig {
            dimension "config"
            applicationId project.ext.buildConfigProperties['android.applicationId'] + "testserver"
            testApplicationId = project.ext.buildConfigProperties['android.applicationId'] + ".testserver.test"
            versionCode rootProject.ext.ustadVersionCode + Integer.parseInt(rootProject.ext.buildConfigProperties['localVersionCode'])
            manifestPlaceholders = ["appName": rootProject.ext.buildConfigProperties['appName'] + " Test Server"]
        }

        vanilla {
            dimension "config"
            applicationId "com.toughra.ustadmobile.testserver"
            testApplicationId "com.toughra.ustadmobile.appandroid.testserver.test"
            versionCode rootProject.ext.ustadVersionCode
            manifestPlaceholders = ["appName": "Ustad Mobile Test Server"]
        }

        /*
         * The vanilla config flavor is here for convenience : it's the same as having no
         * buildconfig.local.properties and relying on what's in buildconfig.default.properties
         *
         * It's just here to make life easier so you can switch flavors in Android studio without
         * having to remove the local config files themselves
         */
        vanillamultidex {
            dimension "config"
            applicationId "com.toughra.ustadmobile.testserver"
            testApplicationId "com.toughra.ustadmobile.appandroid.testserver.test"
            versionCode rootProject.ext.ustadVersionCode
            manifestPlaceholders = ["appName": "Ustad Mobile Test Server"]
            multiDexEnabled true
            packagingOptions { //Required: otherwise we get complaints on assembling
                merge "META-INF/INDEX.LIST"
            }
        }

        devMinApi21 {
            dimension "config"
            minSdkVersion 21
            applicationId "com.toughra.ustadmobile.testserver"
            testApplicationId "com.toughra.ustadmobile.appandroid.testserver.test"
            versionCode rootProject.ext.ustadVersionCode
            manifestPlaceholders = ["appName": "Ustad Mobile Test Server"]
            multiDexEnabled true
            packagingOptions { //Required: otherwise we get complaints on assembling
                merge "META-INF/INDEX.LIST"
            }
        }
    }

}

configurations.all {
    exclude group: "net.sf.kxml", module: "kxml2"
    exclude group: "org.json", module: "json"
    exclude module: "lib-database"

}


dependencies {
    api project(':core')
    api project(':sharedse')
    api project(":app-android")
    api project(":core-tests")
    api project(":sharedse-tests")

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:support-core-utils:$version_android_support_lib"
    implementation "com.android.support:appcompat-v7:$version_android_support_lib"

    testImplementation "junit:junit:$version_junit"
    androidTestImplementation "com.android.support.test:runner:$version_android_junit_runner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$version_android_espresso"

    implementation "org.nanohttpd:nanohttpd:$version_nanohttpd"
    implementation "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"

    /*
     * Included here to avoid build a complaint about different runtime / compile version
     */
    implementation "android.arch.lifecycle:extensions:$version_android_lifecycle"
    annotationProcessor "android.arch.lifecycle:common-java8:$version_android_lifecycle"

}
