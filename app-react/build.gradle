plugins {
    id 'org.jetbrains.kotlin.js'
}

apply plugin: 'kotlinx-serialization'

group = rootProject.group
version = rootProject.version

repositories {
    mavenCentral()
}

kotlin {
    js(LEGACY) {
        browser {
            //Specify distribution bundle output directory
            distribution {
                directory = project.file("build/generated/source/umwebapp")
            }

            binaries.executable()
            webpackTask {
                cssSupport.enabled = true
            }
            runTask {
                cssSupport.enabled = true
            }
            testTask {
                useKarma {
                    useChromeHeadless()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
    }
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation "org.jetbrains.kotlin-wrappers:kotlin-react:$version_kotlin_react"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-react-dom:$version_kotlin_react"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-styled:$version_kotlin_styled"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-react-router-dom:$version_kotlin_react_router_dom"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-redux:$version_kotlin_redux"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-react-redux:$version_kotlin_react_redux"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-mui:$version_kotlin_mui"
    implementation project(":lib-database-mpp")
    implementation project(":core")
    implementation project(":lib-util")
    implementation project(":lib-database")
    implementation project(":lib-database-entities")
    implementation npm("@emotion/react", "$version_npm_emotion")
    implementation npm("@emotion/styled", "$version_npm_emotion")
    implementation npm("moment","$version_npm_moment")
    implementation npm("moment-timezone","$version_npm_moment_timezone")
    implementation npm("@date-io/moment","$version_npm_mement_io")
    implementation npm("@mui/lab","$version_npm_mui_lab")
    implementation npm("@mui/styles","$version_npm_mui_styles")
    implementation npm("@mui/material","$version_npm_mui_material")
    implementation npm("@mui/icons-material","$version_npm_mui_icons")
    implementation npm("date-fns","$version_npm_date_fns")
    implementation npm("html-to-image","$version_npm_html_to_image")
    implementation "com.github.UstadMobile.door:room-annotations:$version_door"
    implementation "org.kodein.di:kodein-di:$version_kodein_di"
    implementation npm("react-google-charts","$version_npm_google_charts")
    implementation "com.github.UstadMobile.door:door-runtime:$version_door"
    implementation "io.ktor:ktor-client-js:$version_ktor"
    implementation "io.ktor:ktor-client-json-js:$version_ktor"
    implementation npm("timezones.json","$version_npm_timezones")
    implementation npm("striptags","$version_npm_striptags")
    implementation npm("mime-matcher","$version_npm_mime_matcher")
    implementation npm("mui-rte","$version_npm_mui_rte")
    implementation npm("draft-js","$version_npm_draft_js")
    implementation npm("draft-js-export-html","$version_npm_draft_js_html_export")
    implementation npm("react-easy-sort","$version_npm_react_easy_sort")
    implementation "com.ustadmobile.xmlpullparserkmp:xmlpullparserkmp:$version_xmlpullparserkmp"
    implementation "io.github.aakira:napier:$version_napier"
}


task copyWebAppBundleToJar(dependsOn: ":app-react:browserDistribution") {
    inputs.files("./src/main/kotlin/*.kt")
    onlyIf {
        /*
         Only run this task if the input files have been updated since the task was last run.
         upToDateWhen was not having the expected effect.
         */
        long inputLastModified = 0L

        rootProject.fileTree(dir: "app-react/src/main/kotlin", include: "**/*.kt").each() { File file ->
            if (file.lastModified() > inputLastModified)
                inputLastModified = file.lastModified()
        }
        File outputFile = project.file("build/generated/source/umwebapp/app-react.js")
        return inputLastModified < outputFile.lastModified()
    }

    doLast{
        copy {
            from project.file("build/generated/source/umwebapp").path
            into rootProject.file("app-ktor-server/src/main/resources/umapp").path
        }
    }
}

task deleteWebAppBundleFromJar{
    doLast{
        def webAppBundleDir = rootProject.file("app-ktor-server/src/main/resources/umapp")
        if(webAppBundleDir.exists()){
            delete webAppBundleDir
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith("clean")) {
        task.dependsOn(deleteWebAppBundleFromJar)
    }
}

tasks.findByPath(":app-ktor-server:shadowJar").mustRunAfter ":app-react:copyWebAppBundleToJar"
