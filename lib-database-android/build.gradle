apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "digital.wup:android-maven-publish:$version_android_maven_publish"
    }
}

android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk


    sourceSets {
        main.java.srcDirs += ["build/generated/source/door"]
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.version_android_target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

/**
 * The Android Kotlin must not have the @JsName annotation that is present in the lib-database
 * project.
 */
task copyLibDatabase(type: Copy) {
    from "../lib-database/src/commonMain/kotlin"
    into "build/generated/source/door"
    include "**/*.kt"
    filter {line -> line.trim().startsWith("@JsName") || (line.trim().startsWith("import")  && line.contains("kotlin.js.JsName")) ? "" : line }
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(copyLibDatabase)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":lib-database-entities")
    implementation project(':lib-door-runtime')
    implementation project(":lib-database-annotations")
    implementation project(':lib-util')

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"

    //ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$version_androidx_lifecycle"
    kapt "androidx.lifecycle:lifecycle-compiler:$version_androidx_lifecycle"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$version_kotlinx_serialization"

    //Room persistence
    implementation "androidx.room:room-runtime:$version_android_room"
    kapt "androidx.room:room-compiler:$version_android_room"

    implementation "androidx.room:room-ktx:$version_android_room"


    // Paging
    implementation "androidx.paging:paging-runtime:$version_androidx_paging"

    implementation "androidx.appcompat:appcompat:$version_androidx_support_lib"
    implementation "androidx.legacy:legacy-support-core-utils:$version_androidx_support_lib"

    testImplementation "junit:junit:$version_junit"
    androidTestImplementation "androidx.test:runner:$version_android_junit_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$version_android_espresso"
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android

            groupId rootProject.group
            artifactId project.name
            version "${rootProject.version}"
        }
    }

    repositories {
        //Set this in projectdir/extrasettings.gradle
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}