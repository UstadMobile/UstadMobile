
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }

        mavenCentral()
    }

    dependencies {
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:$version_gradle_buildconfig_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
    }
}

apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: "maven-publish"
apply plugin: "de.fuerstenau.buildconfig"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    maven {
        url "http://devserver2.ustadmobile.com/repo-mvn"
    }
}

buildConfig {
    appName = "sharedse"

    sourceSets {
        test {
            packageName = "com.ustadmobile.sharedse"
            clsName = "SharedSeTestConfig"
            buildConfigField 'String', 'TESTDB_JDBCURL_UMMAPPDATABASE', rootProject.ext.buildConfigProperties['test.db.UmAppDatabase.url']
            buildConfigField 'String', 'TESTDB_JDBCURL_CLIENTUMAPPDATABASE', rootProject.ext.buildConfigProperties['test.db.clientdb.url']
            buildConfigField 'String', 'TESTDB_JDBCURL_PEERUMAPPDATABASE', rootProject.ext.buildConfigProperties['test.db.peerdb.url']
        }
    }
}

/*
 * Added to overcome limitation on Android studio as per:
 *  http://tools.android.com/knownissues#TOC-JUnit-tests-missing-resources-in-classpath-when-run-from-Studio
 */
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    from "${projectDir}/../core-tests/src/main/resources"
    from "${projectDir}/../sharedse-tests/src/main/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.nanohttpd:nanohttpd:$version_nanohttpd"
    implementation "org.nanohttpd:nanohttpd-nanolets:$version_nanohttpd"
    implementation "com.squareup.okhttp3:okhttp:$version_okhttp"
    implementation "net.sf.kxml:kxml2:$version_kxml";
    implementation "com.google.code.gson:gson:$version_gson"
    implementation "net.lingala.zip4j:zip4j:$version_zip4j"
    implementation "org.json:json:$version_json"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"

    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$version_retrofit"


    implementation project(path: ":core", configuration: "jvmDefault")
    testImplementation project (':lib-database-jdbc')
    implementation project(":lib-database")
    implementation project(":lib-database-runtime")
    compileOnly project(path: ':lib-database-entities', configuration: 'jvmDefault')
    compileOnly project(':core-compile-only')
    implementation project(":lib-util")

    testImplementation "junit:junit:$version_junit"
    testImplementation "org.mockito:mockito-core:$version_android_mockito"
    testImplementation project(":core-tests")
    testImplementation project (':lib-database-entities')
    testImplementation project(":app-javase")
    testImplementation project(":lib-database-runtime-jdbc")


    //SQLite dependencies for running db-driven tests
    testImplementation "com.github.h-thurow:simple-jndi:$version_simple_jndi"
    testImplementation "org.apache.commons:commons-pool2:$version_apache_commons_pool2"
    testImplementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
    testImplementation "org.apache.commons:commons-dbcp2:$version_apache_commons_dbcp2"

    //MockWebServer
    testImplementation "com.squareup.okhttp3:mockwebserver:$version_mockwebserver"

    testImplementation project(":app-javase")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}
